#!/usr/bin/env perl

use strict;
use warnings;

use Getopt::Long       ();
use Pod::Usage         ();

use HTTP::Tiny         ();
use Try::Tiny;
use JSON::Tiny         ();
use Test::Deep::NoTest;
use Data::Dumper       ();

$Data::Dumper::Sortkeys = 1;

my %opt = (
    respect => 1,
);
Getopt::Long::GetOptions(
    \%opt,
    'city=s@',
    'respect!',
    'help',
) || Pod::Usage::pod2usage(1);

Pod::Usage::pod2usage(0) if $opt{help};
Pod::Usage::pod2usage(1) unless $opt{city};

my $locations_cache = [];

my $exit = 0;
$SIG{INT} = sub { $exit = 1 };

logger( 'info', 'ctrl+c to exit' );

while (1) {
    if ($exit) {
        logger( 'info', 'exiting' );
        exit;
    };

    check_available();
    sleep ( $opt{respect} ? 20 : 5 );
}

sub logger {
    my $level  = shift;
    my $string = shift;

    $string = "[" . time . "][$level] " . "$string\n";

    if ( $level eq 'error' ) {
        die $string;
    }

    print $string;

    return;
}

sub check_available {
    my $response = HTTP::Tiny->new->get( 'https://heb-ecom-covid-vaccine.hebdigital-prd.com/vaccine_locations.json' );

    logger( 'error', 'fetch locations: ' . $response->{status} . ' ' . $response->{reason} )
        unless $response->{success};

    my $data = try {
        return JSON::Tiny::decode_json( $response->{content} );
    }
    catch {
        my $exception = $_;
        logger( 'error', "decode JSON: $exception" );
    };

    logger( 'error', 'locations not found' )
        unless $data->{locations};

    return if eq_deeply( $data->{locations}, $locations_cache );

    logger( 'info', 'new appointment data available' );

    $locations_cache = $data->{locations};

    foreach my $location ( @{$data->{locations}} ) {
        next unless grep { uc $_ eq $location->{city} } @{$opt{city}};
        next unless $location->{openAppointmentSlots};

        logger( 'info', 'open appointment slot found:' );
        print Data::Dumper::Dumper( $location );
    }
}

__END__

=pod

=head1 NAME

find-open-vaccination-appointments - find open vaccination appointments at HEB locations

=head1 DESCRIPTION

C<find-open-vaccination-appointments> is a script to check HEB for open
vaccination appointment slots in Texas.

C<find-open-vaccination-appointments> downloads the availability data
from HEB every 20 seconds, then checks the wanted locations for
available appointment slots.

Location, signup URL, and number of availability appointment times and
slots, are output when an availability is found.

This script is a rewrite of @georgebohnisch's nodejs script, in Perl.

=head1 SYNOPSIS

 find-open-vaccination-appointments [--city <houston>] [--city <cypress>]
                                    [--no-respect]
                                    [--help]

=head1 OPTIONS

=over

=item --city

city to get locations from

multiple city options may be defined

=item --no-respect

get the data every 5 seconds instead of every 20

=item --help

print this dialogue

=back

=head1 EXAMPLES

=over

=item check for slots in 2 cities every 5 seconds

 $ ./find-open-vaccination-appointments --city houston --city cypress --no-respect
 [1614970316][info] ctrl+c to exit
 [1614970316][info] new appointment data available
 [1614970316][info] open appointment slot found:
 $VAR1 = {
           'city' => 'HOUSTON',
           'latitude' => '29.92325',
           'longitude' => '-95.19697',
           'name' => 'Summerwood Market H-E-B',
           'openAppointmentSlots' => 1,
           'openTimeslots' => 1,
           'slotDetails' => [
                              {
                                'manufacturer' => 'Moderna',
                                'openAppointmentSlots' => 1,
                                'openTimeslots' => 1
                              }
                            ],
           'state' => 'TX',
           'storeNumber' => 614,
           'street' => '12680 W.LAKE HOUSTON PKWY 8',
           'type' => 'store',
           'url' => 'https://heb.secure.force.com/FlexibleScheduler/FSAppointment?event_ID=[removed]',
           'zip' => '77044-6087'
         };
 [1614970316][info] open appointment slot found:
 $VAR1 = {
           'city' => 'CYPRESS',
           'latitude' => '29.9925',
           'longitude' => '-95.74599',
           'name' => 'Fairfield Market H-E-B',
           'openAppointmentSlots' => 1,
           'openTimeslots' => 1,
           'slotDetails' => [
                              {
                                'manufacturer' => 'Moderna',
                                'openAppointmentSlots' => 1,
                                'openTimeslots' => 1
                              }
                            ],
           'state' => 'TX',
           'storeNumber' => 656,
           'street' => '28550 US- 290',
           'type' => 'store',
           'url' => 'https://heb.secure.force.com/FlexibleScheduler/FSAppointment?event_ID=[removed]',
           'zip' => '77433-4288'
         };
 [1614970364][info] new appointment data available
 [1614970428][info] new appointment data available
 [1614970491][info] new appointment data available

=back

=head1 DEPENDENCIES

=over

=item L<Getopt::Long>

=item L<Pod::Usage>

=item L<HTTP::Tiny>

=item L<Try::Tiny>

=item L<JSON::Tiny>

=item L<Test::Deep::NoTest>

=item L<Data::Dumper>

=back

=cut

